import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'org.jmailen.kotlinter' version '3.3.0'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.livetl.android"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 263
        versionName "6.2.0-beta03"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'variant'
    productFlavors {
        playstore {
            dimension 'variant'
        }
//        fdroid {
//            dimension 'variant'
//            applicationIdSuffix '.fdroid'
//        }
    }

    buildFeatures {
        compose true

        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    lintOptions {
        disable "MissingTranslation"
        abortOnError false
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'

    // DI
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha03'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation 'androidx.core:core-ktx:1.7.0-alpha01'
    implementation "androidx.webkit:webkit:1.4.0"

    // Jetpack Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    implementation 'androidx.navigation:navigation-compose:2.4.0-alpha06'

    // Networking
    final ktor_version = '1.6.0'
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2'

    // Image loading
    final coil_version = '1.3.2'
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-compose:$coil_version"

    final accompanist_version = '0.16.0'
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets-ui:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-material:$accompanist_version"

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.github.tfcporciuncula.flow-preferences:flow-preferences:1.4.0'

    // OSS licenses
    implementation "com.mikepenz:aboutlibraries-core:$about_libs_version"
}

// Requires the submodules to already be initialized, i.e.:
// git submodule update --init --recursive
task buildExtensionSource {
    doLast {
        exec {
            workingDir "../extension/LiveTL"
            commandLine "yarn", "install"
        }
        exec {
            workingDir "../extension/LiveTL"
            commandLine "yarn", "build", "android"
        }
    }
}
task copyExtensionArtifacts(dependsOn: buildExtensionSource, type: Copy) {
    from '../extension/LiveTL/build'
    into './src/main/assets'
    include '**/*'
}

kotlinter {
    experimentalRules = true
}
preBuild.dependsOn(formatKotlin)

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.freeCompilerArgs += [
            "-Xopt-in=kotlin.Experimental",
            "-Xopt-in=kotlin.RequiresOptIn",
            "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
            "-Xuse-experimental=androidx.compose.foundation.ExperimentalFoundationApi",
            "-Xuse-experimental=androidx.compose.material.ExperimentalMaterialApi",
            "-Xuse-experimental=coil.annotation.ExperimentalCoilApi",
            "-Xuse-experimental=com.google.accompanist.navigation.material.ExperimentalMaterialNavigationApi",
    ]
}
